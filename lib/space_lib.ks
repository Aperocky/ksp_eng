FUNCTION GET_THROTTLE {
    PARAMETER DESIRED_ACC IS 20.
    LOCAL MAX_ACC TO SHIP:MAXTHRUST/SHIP:MASS+0.01.
    RETURN MIN(1, DESIRED_ACC/MAX_ACC).
}

FUNCTION PRINT_PARAMS {
    PARAMETER OPTIONAL_ONE IS "".
    PRINT "ALTITUDE:   " + ROUND(SHIP:ALTITUDE) + "  METERS    " AT (5,5).
    PRINT "APOAPSIS:   " + ROUND(SHIP:APOAPSIS) + "  METERS    " AT (5,6).
    PRINT "PERIAPSIS:  " + ROUND(SHIP:PERIAPSIS) + "  METERS    " AT (5,7).
    PRINT "CLIMB RATE: " + ROUND(SHIP:VERTICALSPEED) + "  M/S" AT (5,8).
    PRINT "GRND SPEED: " + ROUND(SHIP:GROUNDSPEED*3.6) + "  KPH" AT (5,9).
    PRINT OPTIONAL_ONE AT (5, 10).
}

FUNCTION DEORBIT_KERBIN {
    LOCK STEERING TO RETROGRADE.
    WAIT UNTIL VANG(RETROGRADE:VECTOR, SHIP:FACING:VECTOR) < 0.25.
    SET THROTTLE TO 1.
    WAIT UNTIL SHIP:PERIAPSIS < 0.
    SET THROTTLE TO 0.
    UNLOCK STEERING.
}

FUNCTION PARACHUTE_LANDING {
    PARAMETER DROGUE IS TRUE.
    SAS OFF.
    LOCK STEERING TO RETROGRADE.
    WAIT 1.
    SET WARP TO 3.
    WAIT UNTIL SHIP:ALTITUDE < 35000.
    SET WARP TO 0.
    WAIT 1.
    ACT_ON_PARTS("SERVICE.BAY", "OPEN").
    IF DROGUE {
        WAIT UNTIL ALT:RADAR < 5000.
        STAGE.
    }
    WAIT UNTIL ALT:RADAR < 2000.
    STAGE.
    WAIT UNTIL ALT:RADAR < 700.
    SET WARP TO 3.
    WAIT UNTIL ALT:RADAR < 20.
    SET WARP TO 0.
    WAIT 5.
    LOCK STEERING TO SHIP:HEADING.
}

// GRAVITY TURN PITCH
FUNCTION GET_PITCH_KERBIN {
    PARAMETER AGGRO IS 15.
    PARAMETER END_INCLINATION IS 15.
    LOCAL END_ALT TO ((90 + AGGRO - END_INCLINATION)/AGGRO) ^ 2 * 1000.
    IF SHIP:ALTITUDE < 1000 {
        RETURN 90.
    }
    IF SHIP:ALTITUDE < END_ALT {
        RETURN 90 + AGGRO - SQRT(SHIP:ALTITUDE/1000) * AGGRO.
    }
    RETURN END_INCLINATION.
}

FUNCTION CIRCULARIZE {
    // USE DUMMY CIRCULARIZATION TECHNIQUE BY KEEPING
    // ONLY USE ON ASCENT!
    // SKID TO APOAPSIS
    WARP_TO_TIME(ETA:APOAPSIS - 10).

    LOCAL PITCH TO 0.
    LOCAL VVDIFF TO 0.
    LOCAL ST TO PROGRADE.
    LOCAL TV TO 0.
    LOCK STEERING TO ST.
    LOCK THROTTLE TO TV.

    WAIT ETA:APOAPSIS.

    UNTIL (SHIP:ALTITUDE - SHIP:PERIAPSIS) < 100 {
        IF STAGE:LIQUIDFUEL < 1 {
            STAGE. WAIT 0.
        }
        SET PITCH TO MIN(30, MAX(0, ORBIT:PERIOD - ETA:APOAPSIS)).
        SET ST TO PROGRADE + R(0, PITCH, 0).
        SET TV TO MIN((SHIP:APOAPSIS - SHIP:PERIAPSIS)/SHIP:APOAPSIS + 0.01, 1).
        PRINT_PARAMS().
        WAIT 0.
    }
    UNLOCK STEERING.
    UNLOCK THROTTLE.
}
