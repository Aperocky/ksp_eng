DECLARE FUNCTION GET_THROTTLE {
    PARAMETER DESIRED_ACC IS 20.
    LOCAL MAX_ACC TO SHIP:MAXTHRUST/SHIP:MASS+0.01.
    RETURN MIN(1, DESIRED_ACC/MAX_ACC).
}

DECLARE FUNCTION PRINT_PARAMS {
    PARAMETER OPTIONAL_ONE IS "".
    PRINT "ALTITUDE:   " + ROUND(SHIP:ALTITUDE) + "  METERS    " AT (5,5).
    PRINT "APOAPSIS:   " + ROUND(SHIP:APOAPSIS) + "  METERS    " AT (5,6).
    PRINT "PERIAPSIS:  " + ROUND(SHIP:PERIAPSIS) + "  METERS    " AT (5,7).
    PRINT "CLIMB RATE: " + ROUND(SHIP:VERTICALSPEED) + "  M/S" AT (5,8).
    PRINT "GRND SPEED: " + ROUND(SHIP:GROUNDSPEED*3.6) + "  KPH" AT (5,9).
    PRINT OPTIONAL_ONE AT (5, 10).
}

DECLARE FUNCTION GRND_INIT {
    SAS OFF.
    RCS OFF.
    GEAR OFF.
}

DECLARE FUNCTION GET_PARTS {
    PARAMETER PART_NAME.
    RETURN SHIP:PARTSDUBBEDPATTERN(PART_NAME).
}

DECLARE FUNCTION LIST_PARTS {
    LOCAL PART_NAMES TO UNIQUESET().
    FOR PART IN SHIP:PARTS {
        PART_NAMES:ADD(PART:NAME).
    }
    FOR PART_NAME IN PART_NAMES {
        PRINT PART_NAME.
    }
}

DECLARE FUNCTION LIST_EVENT_FOR_PART {
    PARAMETER PART.
    LOCAL MODULES TO PART:ALLMODULES.
    FOR MODULE IN MODULES {
        LOCAL EVENTS IS PART:GETMODULE(MODULE):ALLEVENTNAMES.
        FOR EVENT IN EVENTS {
            PRINT EVENT.
        }
    }
}

DECLARE FUNCTION ACT_ON_PART {
    PARAMETER PART.
    PARAMETER ACTION.
    IF ACTION="COLLECT_SCIENCE" { 
        LOCAL EXPERIMENT TO PART:GETMODULE("ModuleScienceExperiment").
        IF EXPERIMENT:HASDATA {
            RETURN.
        }
        EXPERIMENT:DEPLOY.
        WAIT UNTIL EXPERIMENT:HASDATA.
        RETURN.
    }
    LOCAL MODULES TO PART:ALLMODULES.
    FOR MODULE IN MODULES {
        LOCAL EVENTS IS PART:GETMODULE(MODULE):ALLEVENTNAMES.
        FOR EVENT IN EVENTS {
            IF EVENT:CONTAINS(ACTION) {
                PART:GETMODULE(MODULE):DOEVENT(EVENT).
                RETURN.
            }
        }
    }
}

DECLARE FUNCTION ACT_ON_PARTS {
    PARAMETER PART_NAME.
    PARAMETER ACTION_NAME.
    LOCAL PARTS TO GET_PARTS(PART_NAME).
    FOR PART IN PARTS {
        ACT_ON_PART(PART, ACTION_NAME).
    }
}

DECLARE FUNCTION DEORBIT_KERBIN {
    LOCK STEERING TO RETROGRADE.
    WAIT UNTIL VANG(RETROGRADE:VECTOR, SHIP:FACING:VECTOR) < 0.25.
    SET THROTTLE TO 1.
    WAIT UNTIL SHIP:PERIAPSIS < 0.
    SET THROTTLE TO 0.
    UNLOCK STEERING.
}

DECLARE FUNCTION PARACHUTE_LANDING {
    PARAMETER DROGUE IS TRUE.
    SAS OFF.
    LOCK STEERING TO RETROGRADE.
    WAIT 1.
    SET WARP TO 3.
    WAIT UNTIL SHIP:ALTITUDE < 35000.
    SET WARP TO 0.
    WAIT UNTIL SHIP:ALTITUDE < 15000.
    SET WARP TO 3.
    IF DROGUE {
        WAIT UNTIL ALT:RADAR < 5000.
        STAGE.
    }
    WAIT UNTIL ALT:RADAR < 1500.
    STAGE.
    WAIT UNTIL ALT:RADAR < 20.
    SET WARP TO 0.
    WAIT 5.
    LOCK STEERING TO SHIP:HEADING.
}

DECLARE FUNCTION COLLECT_SCIENCE {
    ACT_ON_PARTS("GOO", "COLLECT_SCIENCE").
    ACT_ON_PARTS("BAROMETER", "COLLECT_SCIENCE").
    ACT_ON_PARTS("THERMOMETER", "COLLECT_SCIENCE").
    ACT_ON_PARTS("SCIENCE.MOD", "COLLECT_SCIENCE").
    WAIT 1.
    ACT_ON_PARTS("SCIENCEBOX", "COLLECT").
}

DECLARE FUNCTION GET_PITCH_KERBIN {
    PARAMETER AGGRO IS 15.
    PARAMETER END_INCLINATION IS 15.
    LOCAL END_ALT TO ((90 + AGGRO - END_INCLINATION)/AGGRO) ^ 2 * 1000.
    IF SHIP:ALTITUDE < 1000 {
        RETURN 90.
    }
    IF SHIP:ALTITUDE < END_ALT {
        RETURN 90 + AGGRO - SQRT(SHIP:ALTITUDE/1000) * AGGRO.
    }
    RETURN END_INCLINATION.
}

DECLARE FUNCTION WARP_TO_TIME {
    PARAMETER DTIME.
    PARAMETER RAILS IS TRUE.
    SET TW TO KUNIVERSE:TIMEWARP.
    IF RAILS {
        SET TW:MODE TO "RAILS".
    }
    TW:WARPTO(TIME:SECONDS + DTIME).
    WAIT DTIME.
    WAIT UNTIL TW:WARP = 0 AND TW:ISSETTLED.
}

DECLARE FUNCTION CIRCULARIZE {
    // USE DUMMY CIRCULARIZATION TECHNIQUE BY KEEPING
    // ONLY USE ON ASCENT!
    // SKID TO APOAPSIS
    WARP_TO_TIME(ETA:APOAPSIS - 10).

    LOCAL PITCH TO 0.
    LOCAL VVDIFF TO 0.
    LOCAL ST TO PROGRADE.
    LOCAL TV TO 0.
    LOCK STEERING TO ST.
    LOCK THROTTLE TO TV.

    WAIT ETA:APOAPSIS.

    UNTIL (SHIP:ALTITUDE - SHIP:PERIAPSIS) < 500 {
        SET PITCH TO MIN(30, MAX(0, ORBIT:PERIOD - ETA:APOAPSIS)).
        SET ST TO PROGRADE + R(0, PITCH, 0).
        SET TV TO GET_THROTTLE().
        PRINT_PARAMS().
        WAIT 0.
    }
    UNLOCK STEERING.
    UNLOCK THROTTLE.
}
