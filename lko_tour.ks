// THIS IS A FULL OPERATIONAL SCRIPT CAPABLE OF BRINGING AN VESSEL INTO LKO
// ROUND KERBIN, AND RETURN TO KSC FULLY AUTONOMOUSLY.
PARAMETER BREAKMODE IS "NONE", ORB_ALT IS 85000, STARTMODE IS 1.

RUNONCEPATH("LIB/BASE_LIB.KS").
RUNONCEPATH("LIB/SPACE_LIB.KS").
RUNONCEPATH("LIB/NODE_LIB.KS").
CLEARSCREEN.

SET USE_NODE TO TRUE. // USE IF MANEUVRE MODE IS UNLOCKED.

SET RUNMODE TO STARTMODE.
SET TVAL TO 0.
SET SVAL TO HEADING(90, 90, -90).
LOCK THROTTLE TO TVAL.
LOCK STEERING TO SVAL.

STAGE.
UNTIL RUNMODE=0 {

    IF BREAKMODE=RUNMODE {
        BREAK.
    }

    SET PITCH TO GET_PITCH_KERBIN().
    IF STAGE:LIQUIDFUEL < 1 AND RUNMODE > 1 {
        STAGE. WAIT 0.
    }

    IF RUNMODE=1 {
        SET SVAL TO HEADING(90, PITCH, -90).
        IF STAGE:SOLIDFUEL < 1 {
            STAGE. WAIT 0.
            SET RUNMODE TO 2.
        }
    }

    ELSE IF RUNMODE=2 {
        SET TVAL TO GET_THROTTLE(30).
        SET SVAL TO HEADING(90, PITCH, -90).
        IF SHIP:APOAPSIS > ORB_ALT {
            UNLOCK STEERING.
            SET RUNMODE TO 3.
        }
    }

    ELSE IF RUNMODE=3 {
        SET TVAL TO 0.
        SET WARP TO 3.
        IF SHIP:ALTITUDE > 70000 {
            SET WARP TO 0.
            WAIT 1.
            SET RUNMODE TO 4.
        }
    }

    ELSE IF RUNMODE=4 {
        IF USE_NODE {
            CIRCULARIZE_NODE().
            NODE_EXEC().
        } ELSE {
            CIRCULARIZE().
        }
        SET RUNMODE TO 5.
    }

    ELSE IF RUNMODE=5 {
        WAIT 1.
        LOCAL DEORBIT_LNG TO 33.3.
        IF SHIP_NAME:CONTAINS("LARGE") {
            SET DEORBIT_LNG TO 30.
        }
        LOCAL WAITTIME TO (DEORBIT_LNG + 180 - SHIP:GEOPOSITION:LNG)/360 * ORBIT:PERIOD.
        WARP_TO_TIME(WAITTIME).
        SET RUNMODE TO 6.
    }

    ELSE IF RUNMODE=6 {
        DEORBIT_KERBIN().
        WAIT 1.
        SET WARP TO 3.
        IF SHIP:ALTITUDE < 70000 {
            IF NOT SHIP_NAME:CONTAINS("LARGE") {
                STAGE. WAIT 1.
            }
            SET RUNMODE TO 7.
        }
    }

    ELSE IF RUNMODE=7 {
        PARACHUTE_LANDING(FALSE).
        SET RUNMODE TO 0.
    }

    SET RUNSTR TO "RUNMODE: " + RUNMODE.
    PRINT_PARAMS(RUNSTR).
    WAIT 0.
}

PRINT "LKO TOUR COMPLETE".
