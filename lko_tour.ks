// THIS IS A FULL OPERATIONAL SCRIPT CAPABLE OF BRINGING AN VESSEL INTO LKO
// ROUND KERBIN, AND RETURN TO KSC FULLY AUTONOMOUSLY.
// CAN ALSO BE USED TO START AND END FROM ANY MODE.
// THIS IS PRIMARILY USED TO FERRY TOURISTS FOR AUTO TOURS (WHOLE SCRIPT).
// OR LAUNCH STUFF INTO LKO (BREAK AT MODE 5)
PARAMETER BREAKMODE IS "NONE", ORB_ALT IS 85000, STAGE_WHEN_ORBIT IS TRUE.

RUNONCEPATH("LIB/BASE_LIB.KS").
RUNONCEPATH("LIB/SPACE_LIB.KS").
RUNONCEPATH("LIB/NODE_LIB.KS").
CLEARSCREEN.

SET USE_NODE TO TRUE. // USE IF MANEUVRE MODE IS UNLOCKED.

SET RUNMODE TO 1.
SET TVAL TO 0.
SET SVAL TO HEADING(90, 90, -90).
LOCK THROTTLE TO TVAL.
LOCK STEERING TO SVAL.

RCS OFF.
SAS OFF.
STAGE.
UNTIL RUNMODE=0 {

    IF BREAKMODE=RUNMODE {
        BREAK.
    }

    SET PITCH TO GET_PITCH_KERBIN().
    IF STAGE:LIQUIDFUEL < 1 AND RUNMODE > 1 {
        STAGE. WAIT 0.
    }

    IF RUNMODE=1 {
        SET TVAL TO GET_THROTTLE(25).
        SET SVAL TO HEADING(90, PITCH, -90).
        IF STAGE:SOLIDFUEL < 1 {
            STAGE. WAIT 0.
            SET RUNMODE TO 2.
        }
    }

    ELSE IF RUNMODE=2 {
        SET TVAL TO GET_THROTTLE(20).
        SET SVAL TO HEADING(90, PITCH, -90).
        IF SHIP:APOAPSIS > ORB_ALT {
            UNLOCK STEERING.
            SET RUNMODE TO 3.
        }
    }

    ELSE IF RUNMODE=3 {
        SET TVAL TO 0.
        SET WARP TO 3.
        IF SHIP:ALTITUDE > 70000 {
            SET WARP TO 0.
            ACT_ON_PARTS("FAIRING", "DEPLOY").
            WAIT 1.
            SET RUNMODE TO 4.
        }
    }

    ELSE IF RUNMODE=4 {
        RCS ON.
        IF USE_NODE {
            CIRCULARIZE_NODE().
            NODE_EXEC().
        } ELSE {
            CIRCULARIZE().
        }
        RCS OFF.
        WAIT 1.
        IF GET_PARTS("FAIRING"):LENGTH > 0 AND STAGE_WHEN_ORBIT {
            STAGE.
        }
        ACT_ON_PARTS("VOLTAIC", "EXTEND").
        SET RUNMODE TO 5.
    }

    ELSE IF RUNMODE=5 {
        DEORBIT_KERBIN().
        ACT_ON_PARTS("VOLTAIC", "RETRACT").
        WAIT 1.
        SET WARP TO 3.
        WAIT UNTIL SHIP:ALTITUDE < 70000.
        ACT_ON_PARTS("SEPARATOR", "DECOUPLE").
        ACT_ON_PARTS("DECOUPLER", "DECOUPLE").
        SET RUNMODE TO 6.
    }

    ELSE IF RUNMODE=6 {
        PARACHUTE_LANDING().
        SET RUNMODE TO 0.
    }

    SET RUNSTR TO "RUNMODE: " + RUNMODE.
    PRINT_PARAMS(RUNSTR).
    WAIT 0.
}

CLEARSCREEN.
PRINT "LKO TOUR COMPLETE".
